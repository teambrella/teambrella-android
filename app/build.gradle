buildscript {
    repositories {
        google()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://maven.google.com" }
    mavenCentral()
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}

def keystoreProperties = getProps(rootProject.file("keystore.properties"))

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 27
        versionCode 149
        versionName "and-0.3.14.149"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "app"]
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            v2SigningEnabled false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "default"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {

        development {
            dimension "default"
            def config = getProps(rootProject.file("development.properties"))
            buildConfigField "String", "AUTHORITY", config['Authority'].toString()
            buildConfigField "String", "BASE_URL", config['BaseUrl'].toString()
            buildConfigField "boolean", "isTestNet", config['IsTestNetwork']
            buildConfigField "String", "CP_AUTHORITY", config['ContentProviderAuthority'].toString()
            buildConfigField "String", "SCHEME", "\"http\""
            sourceSets {
                main.res.srcDirs = ['src/main/res', 'src/development/res']
            }
            applicationId config['ApplicationId']
        }

        production {
            dimension "default"
            def config = getProps(rootProject.file("production.properties"))
            buildConfigField "String", "AUTHORITY", config['Authority'].toString()
            buildConfigField "String", "BASE_URL", config['BaseUrl'].toString()
            buildConfigField "boolean", "isTestNet", config['IsTestNetwork']
            buildConfigField "String", "CP_AUTHORITY", config['ContentProviderAuthority'].toString()
            buildConfigField "String", "SCHEME", "\"https\""
            sourceSets {
                main.res.srcDirs = ['src/main/res', 'src/production/res']
            }
            applicationId config['ApplicationId']
        }
    }

    lintOptions {
        abortOnError false
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk false
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.61"

    /* Support */
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-emoji:27.1.1'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"

    /* Server communication */
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'


    implementation 'org.chalup.microorm:microorm:0.8.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    /* Ethereum */
    implementation 'org.bitcoinj:bitcoinj-core:0.14.7'
    //noinspection GradleDependency
    implementation 'org.ethereum:geth:1.6.7'

    /*Image Loader*/
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation("com.github.bumptech.glide:glide:4.7.1") {
        exclude group: "com.android.support"
    }
    implementation("com.github.bumptech.glide:recyclerview-integration:4.7.1") {
        transitive = false
    }

    kapt 'com.github.bumptech.glide:compiler:4.7.1'

    /* *Facebook SDK*/
    implementation 'com.facebook.android:facebook-android-sdk:4.33.0'

    implementation 'com.google.android.gms:play-services-gcm:15.0.1'
    implementation 'com.google.android.gms:play-services-analytics:16.0.1'
    implementation 'com.google.android.gms:play-services-auth:16.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.3.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'


    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'

    implementation 'org.java-websocket:Java-WebSocket:1.3.8'

    /* Auth 0 */
    implementation 'com.auth0.android:auth0:1.13.2'

    /* Test */
    testImplementation 'junit:junit:4.12'

    /* Dagger */
    implementation 'com.google.dagger:dagger-android:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'

    // if you use the support libraries
    kapt 'com.google.dagger:dagger-android-processor:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode =
                project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride =
                    baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}


task copyDevelopmentAppConfig(type: Copy) {
    from rootProject.file("development.xml")
    into file("src/development/res/values")
}

task copyProductionAppConfig(type: Copy) {
    from rootProject.file("production.xml")
    into file("src/production/res/values")
}

task copyDevelopmentGAConfig(type: Copy) {
    from rootProject.file("ga_development.xml")
    into file("src/development/res/xml")
    rename { String fileName ->
        fileName.replace("ga_development.xml", "ga_tracker.xml")
    }
}

task copyProductionGAConfig(type: Copy) {
    from rootProject.file("ga_production.xml")
    into file("src/production/res/xml")
    rename { String fileName ->
        fileName.replace("ga_production.xml", "ga_tracker.xml")
    }
}


preBuild.dependsOn copyDevelopmentAppConfig
preBuild.dependsOn copyProductionAppConfig
preBuild.dependsOn copyDevelopmentGAConfig
preBuild.dependsOn copyProductionGAConfig

apply plugin: 'com.google.gms.google-services'
